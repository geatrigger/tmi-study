## 오류 : no matching manifest for linux/arm64/v8 in the manifest list entries 

* M1 Mac의 경우 ARM64

* Neo4j는 AMD64 아키텍처만 지원

* Rosetta2를 이용

  * Intel 프로세서가 장착된 Mac용으로 제작된 앱을 Apple Silicon이 장착된 Mac에서 사용할 수 있게 만드는 에뮬레이터

    **Rosetta2 설치**

    ``` bash
    softwareupdate --install-rosetta
    ```

    **-platform linux/amd64 neo4j를 붙여 pull**

    ```bash
    docker pull --platform linux/amd64 neo4j
    ```



## arm64에서 돌아가는 Neo4j 이미지

[Neo4j doesn't build or run on the arm64 M1 Macs](https://github.com/neo4j/neo4j/issues/12692#)

#### Official neo4j docker images for ARM은 아직 X 

* Experimental version

  ```bash
  docker run --name myneo4j -p7474:7474 -p7687:7687 --env NEO4J_AUTH='username'/'password' neo4j/neo4j-arm64-experimental:4.2.5-arm64 
  ```

* `latest` tag는 experimental branch에는 붙이지 않고 버젼을 정확히 명시해야함

  ```bash
  docker run -p7474:7474 -p7687:7687 neo4j/neo4j-arm64-experimental --> 이렇게는 pull 불가능 
  ```

* <img src="../../../Screenshot 2021-07-24 at 6.36.49 PM.png" alt="Screenshot 2021-07-24 at 6.36.49 PM" style="zoom:40%;" />

<img src="210728_06_DockerNeo4j실행2.assets/Screenshot 2021-07-24 at 10.17.24 PM.png" alt="Screenshot 2021-07-24 at 10.17.24 PM" style="zoom:25%;" />

<img src="210728_06_DockerNeo4j실행2.assets/Screenshot 2021-07-25 at 9.31.37 AM.png" alt="Screenshot 2021-07-25 at 9.31.37 AM" style="zoom:25%;" />

### Recommendation systems

#### 1. 협업 필터링 (Collaborative filtering)

다른 유저의 성향을 기반으로 추천

기존 영화 평점 데이터를 활용해 한 유저와 영화 취향이 가장 유사한 사람들을 뽑아냄

그리고 유사한 사람들이 평균적으로 높은 평점을 준 영화를 해당 유저에게 추천

e.g. 나는 영화 그래비티를 좋아함. 그래비티를 좋아하는 또 다른 유저가 좋아한 영화 인터스텔라를 나에게 추천 

#### 2. 콘텐츠 기반 필터링 (content-based filtering) 

'content'의 유사성 기반으로 추천

'어떤 영화를 좋아한 유저는 이와 유사한 영화를 좋아할 것이다'라는 가정 

e.g. 나는 영화 그래비티를 좋아함. 그래비티의 키워드는 '우주여행', 'SF' 등..이와 비슷한 키워드를 가진 영화 인터스텔라를 나에게 추천 



## movie recommendation cyper 

Tom Hanks가 출연한 영화 (tom&movie graph)

```cypher
MATCH (tom:Person{name:'Tom Hanks'})-[r:ACTED_IN]->(movie:Movie) RETURN tom, movie
```

movie만 return한다면 nodes는 나오지만 relationships은 보여주지 않음 

```cypher
MATCH (tom:Person{name:'Tom Hanks'})-[r:ACTED_IN]->(movie:Movie) RETURN movie
```

Tom Hanks가 출연한 영화에 같이 출연한 모든 person의 이름 

```cypher
MATCH (tom:Person{name:'Tom Hanks'})-[r:ACTED_IN]->(:Movie)<-[:ACTED_IN]-(coActor:Person) RETURN coActor.name
```

Tom Hanks가 출연한 영화1에 같이 출연한 Person1 (coActor)

위의 Person이 출연한 영화2에 같이 출연한 Person2 (coCoActor)

coCoActor != Tom Hanks

coCoActor와 Tom Hanks가 같이 출연한 영화를 제외한 나머지 영화에 출연한 모든 person의 이름 

```cypher
MATCH (tom:Person {name: 'Tom Hanks'})-[:ACTED_IN]->(movie1:Movie)<-[:ACTED_IN]-(coActor:Person)-[:ACTED_IN]->(movie2:Movie)<-[:ACTED_IN]-(coCoActor:Person)
WHERE tom <> coCoActor
AND NOT (tom)-[:ACTED_IN]->(:Movie)<-[:ACTED_IN]-(coCoActor)
RETURN coCoActor.name
```

위 방법의 문제점 : 같은 이름이 연속적으로 나타남 (e.g. Keanu Reeves)

같은 이름이 나타난 빈도 (횟수)로 counting을 하는 게 더 나은 방법 (count() 사용)

```cypher
MATCH (tom:Person {name: 'Tom Hanks'})-[:ACTED_IN]->(movie1:Movie)<-[:ACTED_IN]-(coActor:Person)-[:ACTED_IN]->(movie2:Movie)<-[:ACTED_IN]-(coCoActor:Person)
WHERE tom <> coCoActor
AND NOT (tom)-[:ACTED_IN]->(:Movie)<-[:ACTED_IN]-(coCoActor)
RETURN coCoActor.name, count(coCoActor) as frequency
ORDER BY frequency DESC
LIMIT 10
```

### 그래프로 보기

```cypher
MATCH (tom:Person {name: 'Tom Hanks'})-[:ACTED_IN]->(movie1:Movie)<-[:ACTED_IN]-(coActor:Person)-[:ACTED_IN]->(movie2:Movie)<-[:ACTED_IN]-(Reeves:Person {name: 'Keanu Reeves'})
WHERE NOT (tom)-[:ACTED_IN]->(:Movie)<-[:ACTED_IN]-(Reeves)
RETURN tom, movie1, coActor, movie2, Reeves
```

<img src="210728_06_DockerNeo4j실행2.assets/Screenshot 2021-07-25 at 11.02.10 AM.png" alt="Screenshot 2021-07-25 at 11.02.10 AM" style="zoom:25%;" />



### movie recommendation data set

### 콘텐츠 기반 필터링

#### 영화 장르 확인하기

```cypher
MATCH (m:Movie {title: "Gravity"})-[:IN_GENRE]->(g:Genre)
RETURN g
```

```cypher
MATCH p=(m:Movie {title: "Gravity"})-[:IN_GENRE]-()
RETURN p
```

* 노드, 관계 색상 크기 변경 가능

#### 비슷한 영화 장르로 가장 유사한 영화 찾기 

```cypher
MATCH (m:Movie)-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(rec:Movie) WHERE m.title = 'Gravity' WITH rec, COLLECT(g.name) AS genres, COUNT(*) AS commonGenres 
RETURN rec.title, genres, commonGenres
ORDER BY commonGenres DESC LIMIT 20
```

#### 특정 개인을 위한 선호 장르 기반 추천 영화 찾기 

```cypher
MATCH (u:User)-[r:RATED]->(m:Movie), (m)-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(rec:Movie) WHERE u.name = 'Stacy Grant' and NOT EXISTS ((u)-[:RATED]->(rec)) 
                                                                                                                                    WITH rec, [g.name, COUNT(*)] AS scores 
RETURN rec.title AS recommendation, rec.imdbRating AS rating, rec.year AS year, COLLECT(scores) AS scoreComponents, REDUCE (s=0, x in COLLECT(scores)|s+x[1]) AS score ORDER BY score DESC LIMIT 20
```

#### 가중치 부여

```cypher
MATCH (m:Movie)-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(rec:Movie)
WHERE m.title = 'Gravity'
WITH m, rec, COUNT(*) AS gs

OPTIONAL MATCH (m)<-[:ACTED_IN]-(a:Actor)-[:ACTED_IN]->(rec)
WITH m, rec, gs, COUNT(a) AS as

OPTIONAL MATCH (m)<-[:DIRECTED]-(d:Director)-[:DIRECTED]->(rec)
WITH m, rec, gs, as, COUNT(d) AS ds

RETURN rec.title AS recommendation, (5*gs)+(3*as)+(4*ds) AS score ORDER BY score DESC LIMIT 20
```



Neo4j와 Cyper를 사용해 콘텐츠 기반 필터링 영화 추천 모델 예제 수행. 노드와 노드 사이의 연결을 그래프로 잘 나타낼 수 있다는 장점. 콘텐츠 기반 필터링은 콘텐츠 자체를 분석하기 때문에 더 면밀한 콘텐츠 tagging을 통하여 더욱 정교한 추천 가능



정리 :

**Content-based filtering 모델을 간단하게 만들어보았고 Neo4j의 장점인 노드와 노드 사이의 연결을 그래프로 직관적이게 시각화 수행함. 다른 Nosql과 비교했을 때는 아직까지는 시각화의 장점만 보임**

주제 :

**추천시스템(협업필터링과 콘텐츠 기반 필터링정리).Neo4j와 Cyper를 사용해 콘텐츠 기반 필터링 영화 추천 모델 예제수행**
