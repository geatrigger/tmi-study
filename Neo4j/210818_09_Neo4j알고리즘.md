노드 불러오기

```cypher
LOAD CSV WITH HEADERS FROM'https://raw.githubusercontent.com/YJKim061401/TIL/master/python/Neo4j/data_transport-nodes.csv' AS row MERGE (place:Place {id:row.id})
                                                                                                                                                      SET place.latitude = toFloat(row.latitude), place.longitude = toFloat(row.longitude),place.popluation = toInteger(row.population);
```

관계 불러오기

```cypher
LOAD CSV WITH HEADERS
FROM'https://raw.githubusercontent.com/YJKim061401/TIL/master/python/Neo4j/data_transport-relationships.csv' AS row
MATCH (origin:Place {id: row.src})
MATCH (destination: Place {id:row.dst})
MERGE (origin)-[:EROAD{distance:toInteger(row.cost)}]->(destination);
```

그래프 확인

```cypher
CALL db.schema.visualization
```

```cypher
MATCH (n:Place) RETURN n
```

<img src="다음스턷.assets/Screenshot 2021-08-14 at 3.32.09 PM.png" alt="Screenshot 2021-08-14 at 3.32.09 PM" style="zoom:50%;" />



### Shortest Path

1. 노드 기준으로 계산 (Unweighted Shortest Path)

   * 노드사이의 cost를 무시하고 노드 사이에 거치는 횟수만 

   Amsterdam에서 London까지 2개의 노드 거쳐 3번 이동
   
   ```cypher
   MATCH (start:Place {id: "Amsterdam"}),
    (destination:Place {id: "London"})
   CALL gds.alpha.shortestPath.stream({
    startNode: start, # 위에서 할당한 start를 startnode로
    endNode: destination, # destination을 endnode로 
    nodeProjection: "*", # 모든 노드를 사용하겠다 (*)
    relationshipProjection: {
    all: {
    type: "*",
    orientation: "UNDIRECTED" # 방향성은 무시 
    }
    }
   })
   YIELD nodeId, cost
   RETURN gds.util.asNode(nodeId).id AS place, cost;
   ```

   <img src="다음스턷.assets/Screenshot 2021-08-14 at 4.00.23 PM.png" alt="Screenshot 2021-08-14 at 4.00.23 PM" style="zoom:50%;" />

   * gds.alpha.shortestpath.stream : 중간 결과를 보여줌
   * gds.alpha.shortestpath.write : 최종 결과만 보여줌
   
   ```cypher
   MATCH (start:Place {id: "Amsterdam"}),
    (destination:Place {id: "London"})
   CALL gds.alpha.shortestPath.write({
    startNode: start,
    endNode: destination,
    nodeProjection: "*",
    relationshipProjection: {
    all: {
    type: "*",
    orientation: "UNDIRECTED"
    }
    }
   })
   YIELD nodeCount
   RETURN nodeCount
   
   ```
   
   <img src="210818_09_Neo4j알고리즘.assets/Screenshot 2021-08-18 at 7.12.27 PM.png" alt="Screenshot 2021-08-18 at 7.12.27 PM" style="zoom:50%;" />
   
   횟수가 아닌 실제 거리값을 return하는 코드
   
   ```cypher
   MATCH (start:Place {id: "Amsterdam"}),
    (destination:Place {id: "London"})
   CALL gds.alpha.shortestPath.stream({
    startNode: start,
    endNode: destination,
    nodeProjection: "*",
    relationshipProjection: {
    all: {
    type: "*",
    orientation: "UNDIRECTED"
    }
    }
   })
   YIELD nodeId, cost
   WITH collect(gds.util.asNode(nodeId)) AS path
   # collect괄호안의 내용을 path라는 list로 정의 
    
   UNWIND range(0, size(path)-1) AS index
   # range 각 값을 index로 정의 (loop시작)
                                
   WITH path[index] AS current, path[index+1] AS next
   # 현재 index에 해당하는 path(node)를 current로
   # 그 다음 index에 해당하는 path(node)를 next로 할당
                                     
   WITH current, next, [(current)-[r:EROAD]-(next) | r.distance][0] AS distance
   # current와 next사이의 relation의 0번 값을 distance로 정의   
     
   WITH collect({current: current, next:next, distance: distance}) AS stops
   # 위에서 정의했던 값들로 stops라는 dict정의               
                  
   UNWIND range(0, size(stops)-1) AS index
   # stops의 index 정의 (loop 시작)
                            
   WITH stops[index] AS location, stops, index
   # index에 해당하는 stops는 location으로 할당, stops, index 사용하여
   
   RETURN location.current.id AS place,
    reduce(acc=0.0,
    distance in [stop in stops[0..index] | stop.distance] |
    acc + distance) AS cost;
    # location의 current id를 place로 return
    # stops안의 stop에서 distnace를 acc에 더해 cost로 할당 
   ```



<img src="다음스턷.assets/Screenshot 2021-08-14 at 4.00.42 PM.png" alt="Screenshot 2021-08-14 at 4.00.42 PM" style="zoom:50%;" />



2. 노드 사이의 실제 거리 값을 계산 (Weighted Shortest Path) 

   ```cypher
               
   MATCH (source:Place {id: "Amsterdam"}),
         (destination:Place {id: "London"})
   
   CALL gds.alpha.shortestPath.stream({
     startNode: source,
     endNode: destination,
     nodeProjection: "*",
     relationshipProjection: {
       all: {
         type: "*",
         properties: "distance",
         orientation:  "UNDIRECTED"
       }
     },
     relationshipWeightProperty: "distance"
   })
   YIELD nodeId, cost
   RETURN gds.util.asNode(nodeId).id AS place, cost;                   
                          
   ```
   
   * relationshipWeightProperty 
     * 노드 한쌍 사이를 거치는데 소요되는 비용인 관계 속성
     * 비용 : 두 위치 사이의 거리 (킬로미터)
   * ![Screenshot 2021-08-14 at 4.01.53 PM](다음스턷.assets/Screenshot 2021-08-14 at 4.01.53 PM.png)

### A*

위도 경도 추가 

```cypher
MATCH (start:Place {id: "Amsterdam"}),
	(destination:Place {id: "London"})
CALL gds.alpha.shortestPath.astar.stream({
	startNode: start,
	endNode: destination,
	nodeProjection: {
		Place: {
			properties: ['longitude', 'latitude']
		}
	},
	relationshipProjection: {
		all: {
			type: "*",
			properties: "distance",
			orientation: "UNDIRECTED"
		}
	},
    	relationshipWeightProperty: "distance",
        propertyKeyLat: "latitude",
        propertyKeyLon: "longitude"
})
YIELD nodeId, cost
RETURN gds.util.asNode(nodeId).id AS place, cost;

```

* 추가된 속성값 (해당 노드와 도착 노드까지의 거리를 구하기 위해서)
  * propertyKeyLat : 각 노드의 위도를 나타내기 위해서 사용된 속성의 이름
  * propertyKeyLon : 각 노드의 경도를 나타내기 위해서 사용된 속성의 이름

<img src="다음스턷.assets/Screenshot 2021-08-14 at 4.01.53 PM.png" alt="Screenshot 2021-08-14 at 4.01.53 PM" style="zoom:67%;" />

이보다 복잡한 데이터셋에서는 A* 알고리즘이 더 빠름 

### Yen’s k-Shortest Paths

* 두쌍의 노드사이의 최단거리 + n번째까지의 최단거리 

```cypher
MATCH (start:Place {id:"Amsterdam"}),
      (end:Place {id:"London"})

CALL gds.alpha.kShortestPaths.stream({
  startNode: start,
  endNode: end,
  nodeProjection: "*",
  relationshipProjection: {
    all: {
      type: "*",
      properties: "distance",
      orientation:  "UNDIRECTED"
    }
  },
  relationshipWeightProperty: "distance",
  k: 5
})

YIELD index, sourceNodeId, targetNodeId, nodeIds, costs, path
RETURN index,
       [node in gds.util.asNodes(nodeIds[1..-1]) | node.id] AS via,
       reduce(acc=0.0, cost in costs | acc + cost) AS totalCost;
```

<img src="210818_09_Neo4j알고리즘.assets/Screenshot 2021-08-18 at 8.05.29 PM.png" alt="Screenshot 2021-08-18 at 8.05.29 PM" style="zoom:50%;" />







참고 : https://www.oreilly.com/library/view/graph-algorithms/9781492047674/ch04.html

### pathfinding알고리즘

![gral rr 0401](https://www.oreilly.com/library/view/graph-algorithms/9781492047674/assets/gral_rr_0401.png)





### Neo4j 플러그인

#### Neo4j Graph Data Science Library

* 그래프 알고리즘에 집중된 다양한 분석을 제공
  * 커뮤니티 디텍션 --> 소셜 네트워크 분석에 활용
  * 중앙화
  * 노드 유사도
  * 길 찾기
  * 연결점 예측
  * 데이터 사이언스 워크 플로우 
  * 머신러닝

<img src="다음스턷.assets/Screenshot 2021-08-14 at 8.12.53 PM.png" alt="Screenshot 2021-08-14 at 8.12.53 PM" style="zoom:33%;" />

<img src="다음스턷.assets/Screenshot 2021-08-14 at 8.12.58 PM.png" alt="Screenshot 2021-08-14 at 8.12.58 PM" style="zoom:33%;" /><img src="다음스턷.assets/Screenshot 2021-08-14 at 8.13.29 PM.png" alt="Screenshot 2021-08-14 at 8.13.29 PM" style="zoom:33%;" />

